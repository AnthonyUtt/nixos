{
  stdenv,
  lib,
  fetchFromGitHub,
  writeScript,
  coreutils,
  neovim,
  ripgrep,
  fd,
  git,
  nodejs_18,
  ruby_3_3,
  cargo,
  wget,
}:
let
  pname = "nvchad";
  version = "2.0";

  src = fetchFromGitHub {
    owner = "nvchad";
    repo = "nvchad";
    rev = "refs/heads/v${version}";
    sha256 = "sha256-tKMvKdB3jPSvcyewaOe8oak3pXhjAcLyyxgGMiMeqeU=";
  };

  launcher = writeScript "nvchad" ''
    export PATH="${lib.makeBinPath [ coreutils neovim ripgrep fd git nodejs_18 ruby_3_3 cargo wget ]}"
    export XDG_CONFIG_HOME=$(mktemp -d)

    # FIXME: Use the real XDG_CONFIG_HOME or fallback to $HOME/.config
    mkdir -p $HOME/.config/nvchad

    # Set up disposable XDG_CONFIG_HOME
    mkdir $XDG_CONFIG_HOME/nvim
    ln -s ${src}/LICENSE $XDG_CONFIG_HOME/nvim/LICENSE
    ln -s ${src}/README.md $XDG_CONFIG_HOME/nvim/README.md
    ln -s ${src}/init.lua $XDG_CONFIG_HOME/nvim/init.lua
    cp -r ${src}/lua $XDG_CONFIG_HOME/nvim/lua
    chmod u+w $XDG_CONFIG_HOME/nvim/lua
    ln -s $HOME/.config/nvchad $XDG_CONFIG_HOME/nvim/lua/custom

    exec nvim "$@"
  '';
in
stdenv.mkDerivation {
  inherit src version pname;

  installPhase = ''
    mkdir -p $out/bin
    ln -s ${launcher} $out/bin/nvchad
  '';
}
